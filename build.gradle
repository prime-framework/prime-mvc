apply plugin: 'java'

group = "org.primeframework"
version = "1.0-SNAPSHOT"
description = "Prime MVC Framework"
sourceCompatibility = 1.6

configurations {
  compileOnly {
    visible = false
  }
  testCompile.extendsFrom compileOnly
}

dependencies {
  compileOnly(
    "javax.servlet:servlet-api:2.4"
  )

  compile(
    "com.google.inject:guice:3.0",
    "com.google.inject.extensions:guice-multibindings:3.0",
    "commons-fileupload:commons-fileupload:1.2.1",
    "commons-io:commons-io:1.3.2",
    "javax.validation:validation-api:1.0.0.GA",
    "joda-time:joda-time:2.1",
    "org.apache.commons:commons-lang3:3.0.1",
    "org.freemarker:freemarker:2.3.16",
    "org.primeframework:prime-mock:1.0-SNAPSHOT"
  )

  testCompile(
    "org.easymock:easymock:3.1",
    "org.hibernate:hibernate-validator:4.2.0.Final",
    "org.testng:testng:6.3.1"
  )
}

repositories {
  mavenCentral()
  ivy {
    name = "ivyLocal"
    url "${project.gradle.gradleUserHomeDir}/integration-cache"
  }
}

uploadArchives {
  repositories {
    add project.repositories.ivyLocal
  }
}

// gradle doesn't provide the capability to define 'compile only'
// dependencies (e.g maven 'provided') so this script-block below adds it
sourceSets {
  main {
    compileClasspath += configurations.compileOnly
  }
}

task srcJar(type: Jar) {
  from sourceSets.main.java
  classifier = "sources"
}

artifacts {
  archives srcJar
}

test {
  useTestNG()
}

compileJava.doLast {
  copy {
    from "src/main/ftl"
    into "build/classes/main"
  }
}
